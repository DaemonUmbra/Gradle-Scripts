//Begin Internal Project Dep Management

/*
These blocks are meant only to create, arrange, and interlink sourceSets
Anything to do with external dependencies should go in other blocks
 */
sourceSets {
    api
    generated
    main
    test
}

configurations {
    //API
    apiImplementation.extendsFrom minecraft

    //GENERATED
    generatedImplementation.extendsFrom apiImplementation
    generatedCompileOnly.extendsFrom apiCompileOnly
    generatedCompileClasspath.extendsFrom apiCompileClasspath
    generatedAnnotationProcessor.extendsFrom apiAnnotationProcessor
    generatedRuntimeOnly.extendsFrom apiRuntimeOnly
    generatedRuntimeClasspath.extendsFrom apiRuntimeClasspath

    //MAIN
    implementation.extendsFrom generatedImplementation
    compileOnly.extendsFrom generatedCompileOnly
    compileClasspath.extendsFrom generatedCompileClasspath
    annotationProcessor.extendsFrom generatedAnnotationProcessor
    runtimeOnly.extendsFrom generatedRuntimeOnly
    runtimeClasspath.extendsFrom generatedRuntimeClasspath

    //TEST
    testImplementation.extendsFrom implementation
    testCompileOnly.extendsFrom compileOnly
    testCompileClasspath.extendsFrom compileClasspath
    testAnnotationProcessor.extendsFrom annotationProcessor
    testRuntimeOnly.extendsFrom runtimeOnly
    testRuntimeClasspath.extendsFrom compileClasspath
}

dependencies {
    generatedImplementation sourceSets.api.output
    implementation sourceSets.generated.output
    testImplementation sourceSets.main.output
}

//End Internal Project Dep Management

//Begin task creation and configuration

task apiJar(type: Jar) {
    group 'build'
    description jar.description.replace("main", "api")
    classifier 'api'
    from sourceSets.api.output
}

jar {
    from sourceSets.api.output
    from sourceSets.generated.output
}

task sourcesJar(type: Jar){
    group 'build'
    description 'Assembles a jar archive containing the source files'
    from sourceSets.api.allSource
    from sourceSets.generated.allSource
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    group = 'build'
    description 'Assembles a jar containing the javadocs'
    from javadoc
    classifier = 'javadoc'
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

//End task creation and configuration

afterEvaluate {
    for (SourceSet sourceSet : sourceSets.getAsMap().values()) {
        for (File file : sourceSet.allSource.srcDirs) {
            file.mkdirs()
        }
        for (File file : sourceSet.resources.srcDirs) {
            file.mkdirs()
        }
    }
}

//Begin Util methods

ext.loadProperties = { File propsFile ->
    Properties properties = new Properties()
    propsFile.withInputStream {
        properties.load(it)
        properties.each {
            ext.set(it.key, it.value)
        }
    }
}

//End Util Methods
