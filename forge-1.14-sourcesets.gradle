//Begin Internal Project Dep Management
/*
These blocks are meant only to create, arrange, and interlink sourceSets
Anything to do with external dependencies should go in other blocks
 */
Logger logger = getLogger()
String keyPrefix = "net.daemonumbra.forge-1.14-sourceset."
String hideDescriptionKey = keyPrefix + "disableDescription"
String fatJarKey = keyPrefix + "fatJar"
String autorunApiJarKey = keyPrefix + "autorun.api"
String autorunSourcesJarKey = keyPrefix + "autorun.sources"

boolean isKeyTrue(String property) {
    if (project.properties.containsKey(property)) {
        return (project.properties.get(property) as String).toLowerCase(Locale.ROOT) == "true"
    } else if (System.getenv().containsKey(property))
        return System.getenv(property).toLowerCase(Locale.ROOT) == "true"
    else
        return false
}

logger.log(LogLevel.LIFECYCLE, "Running DaemonUmbra's \"forge-1.14-sourceset\" script\n")

if (!project.plugins.findPlugin('java')) {
    logger.log(LogLevel.WARN, "DaemonUmbra's \"forge-1.14-sourceset\" script requires the \'java\' plugin to function")
    return
}
if (!isKeyTrue(hideDescriptionKey)) {
    logger.log(LogLevel.LIFECYCLE,
        "To Disable this message set \"${hideDescriptionKey}\" to true as a project property or environment variable\n" +
        "To have this script set jar to autorun apiJar, set \"${autorunApiJarKey}\" to true as a project property or environment variable\n" +
        "To have this script set jar to autorun sourcesJar, set \"${autorunSourcesJarKey}\" to true as a project property or environment variable\n" +
        "To have this script set jar to include the api classes, set \"${fatJarKey}\" to true as a project property or enfironment variable (implies autorun)"
    )
}
if (project.plugins.findPlugin('maven-publish')) {
    logger.log(LogLevel.WARN,
        "this script may cause a \"cannot configure the \'publishing\' extension after it has been accessed\" error,\n" +
        "to fix this please make sure you apply maven-publish after all other plugins and scripts")
}

sourceSets {
    api
    generated
}

configurations {
    //API
    apiImplementation.extendsFrom minecraft

    //GENERATED
    generatedImplementation.extendsFrom apiImplementation
    generatedCompileOnly.extendsFrom apiCompileOnly
    generatedCompileClasspath.extendsFrom apiCompileClasspath
    generatedAnnotationProcessor.extendsFrom apiAnnotationProcessor
    generatedRuntimeOnly.extendsFrom apiRuntimeOnly
    generatedRuntimeClasspath.extendsFrom apiRuntimeClasspath

    //MAIN
    implementation.extendsFrom generatedImplementation
    compileOnly.extendsFrom generatedCompileOnly
    compileClasspath.extendsFrom generatedCompileClasspath
    annotationProcessor.extendsFrom generatedAnnotationProcessor
    runtimeOnly.extendsFrom generatedRuntimeOnly
    runtimeClasspath.extendsFrom generatedRuntimeClasspath

    //TEST
    testImplementation.extendsFrom implementation
    testCompileOnly.extendsFrom compileOnly
    testCompileClasspath.extendsFrom compileClasspath
    testAnnotationProcessor.extendsFrom annotationProcessor
    testRuntimeOnly.extendsFrom runtimeOnly
    testRuntimeClasspath.extendsFrom compileClasspath
}

dependencies {
    generatedImplementation sourceSets.api.output
    implementation sourceSets.generated.output
    testImplementation sourceSets.main.output
}

task apiJar(type: Jar) {
    group 'build'
    description jar.description.replace("main", "api")
    classifier 'api'
    from sourceSets.api.output
}

task sourcesJar(type: Jar){
    group 'build'
    description 'Assembles a jar archive containing the source files'
    from sourceSets.api.allJava
    from sourceSets.main.allJava
    classifier 'sources'
}

jar {
    if (isKeyTrue(autorunApiJarKey) || isKeyTrue(fatJarKey))
        dependsOn tasks.getByName('apiJar')
    if (isKeyTrue(autorunSourcesJarKey))
        dependsOn tasks.getByName('sourcesJar')
    if (isKeyTrue(fatJarKey))
        from sourceSets.api.output
}

afterEvaluate {
    for (SourceSet sourceSet : sourceSets.getAsMap().values()) {
        for (File file : sourceSet.java.srcDirs) {
            file.mkdirs()
        }
        for (File file : sourceSet.resources.srcDirs) {
            file.mkdirs()
        }
    }
}

//End Internal Project Dep Management